// TODO: Add more comments to this file
import Array;
import flixel.FlxG;
import funkin.data.song.SongRegistry;
import funkin.modding.module.Module;
import funkin.modding.module.ModuleHandler;
import funkin.modding.PolymodErrorHandler;
import funkin.Paths;
import funkin.play.song.Song;
import funkin.util.assets.DataAssets;
import funkin.util.Constants;
import funkin.util.tools.ArrayTools;
import openfl.utils.Assets;
import String;
import StringTools;

/**
 * In order for this to work, the base
 * song's script needs to use the
 * template on docs/templates/SongScriptTemplate.hxc
 */
class VariationInjector extends Module {

    function new() {
        super('vari-injector', 0);
    }

    // PLEASE DON'T MODIFY THIS VARIABLE!!!
    /**
     * Used for tracking setup completion.
     */
    var finishedSetup:Bool = false;

    /**
     * Tries to injects a new variation into the song.
     * @param variId the variation id to inject.
     * @param songId the song id to inject the variation to.
     */
    function injectVariationToSong(variId:String, songId:String):Void {
        var entry = SongRegistry.instance.fetchEntry(songId);
        if (entry == null) {return;}
        if (entry.variations.contains(variId)) {return;}

        // fetch the requested variation's
        // metadata for the song
        var variMeta = entry.fetchVariationMetadata(songId, variId);
        // if it doesn't exist, the
        // variation injection failed
        if (variMeta == null) {
            FlxG.log.warn('[vari-injector]  -  Failed to load variation metadata ('+songId+':'+variId+'), is the path correct?');
            return;
        }
        // add the new variation
        // metadata to the _metadata map
        entry._metadata.set(variMeta.variation, variMeta);
        trace('[vari-injector]  -  Successfully injected variation to song '+'('+variId+' => '+songId+')');
    }

    function setup():Void {
        if (this.finishedSetup) {return;}

        var UDPaths = ModuleHandler.getModule("ud-paths");

        // gets all the variation injection files.
        var variIdList:Array<String> = DataAssets.listDataFilesInPath(
            UDPaths.scriptCall("variationInjection"),'.txt'
        ).map(function(variInjectPath:String):String {
            return variInjectPath.split('/')[0];
        });
        // if a file is named "default.txt", it will be ignored,
        // because you can't inject the default variation to a song,
        // it'll always be there.
        variIdList.remove("default");
        
        // For each variation injection file,
        // parse it and inject the variation to
        // each song that needs it.
        for (variId in variIdList) {
            var path:String = Paths.txt(UDPaths.scriptCall("variationInjection", [variId]));
            if (!Assets.exists(path)) {continue;}
            for (songId in Assets.getText(path).split("\n").map(function(e:String):String {return StringTools.trim(e);})) {
                if (songId == "") {continue;}
                if (StringTools.startsWith(songId, "//")) {continue;}
                this.injectVariationToSong(variId, songId);
            }
        }
        trace("[vari-injector]  -  Finished injecting variations!");

        this.finishedSetup = true;
    }


    public override function onCreate(event:ScriptEvent):Void {
        super.onCreate();
        setup();
    }

    public override function onStateChangeEnd(event:StateChangeScriptEvent):Void {
        super.onStateChangeEnd();
        setup();
    }
}